GWENDOLEN 

		:name: exec 

		:Initial Beliefs:
			flightPhase(unknown) 
			destination(sumburgh) 
			
		:Reasoning Rules:
		
		atc_message(atc(M));
		flightPhase_message(veh(status, F));
		fuel_level_message(fuel(level, F));
		position_message(position(Lat, Lon, Alt));
		equal(A, A);
		das_alert :- das(objectApproaching);
		das_alert :- das(alert500);
		stationary :- flightPhase(waitingAtRamp);
		stationary :- flightPhase(holding);
		stationary :- flightPhase(linedup);
		stationary :- flightPhase(landed);
		on_ground :- stationary;
		on_ground :- flightPhase(taxying);
		on_gorund :- flightPhase(lineup);
		das_alert_irrelevant :- on_ground, das(alert500);
		das_alert_irrelevant :- stationary;
		environment_flight_phase(linedup);
		environment_flight_phase(holding);
		environment_flight_phase(cruise);
		environment_flight_phase(waitingAtRampe);
		
		routes_stored_for_enaction :- enactRoute(taxi, N), enactRoute(cruise, N1);
		missionComplete :- destination(L), location(L), flightPhase(waitingAtRamp);
		completeLandingChecks :- fuel(level, L), position(Lat, Long, Alt), flightPhase(landed);

		:Initial Goals: 
			missionComplete [achieve] 
					
		:Plans: 
		
		+.received(:tell, veh(status, NewFlightPhase)): {B flightPhase(OldFlightPhase)} <- 
			+.lock,
			+!changeFlightPhase(NewFlightPhase) [perform],
			-.lock;
		+.received(:tell, atc(M)) : {B atc(N)} <-
			+.lock,
			-atc(N),
			+atc(M),
			-.lock;
		+.received(:tell, atc(M)) : {~ B atc(N)} <-
			+.lock,
			+atc(M),
			-.lock;
		+.received(:tell, fuel(level, M)) : {B fuel(level, N)} <-
			+.lock,
			-fuel(level, N),
			+fuel(level, M),
			-.lock;
		+.received(:tell, fuel(level, M)) : {~ B fuel(level, N)} <-
			+.lock,
			+fuel(level, M),
			-.lock;
		+.received(:tell, position(Lat, Lon, Alt)) : { B position(Lat1, Lon1, Alt1)} <-
			+.lock,
			-position(Lat1, Lon1, Alt1),
			+position(Lat, Lon, Alt),
			-.lock;
		+.received(:tell, position(Lat, Lon, Alt)) : {~ B position(Lat1, Lon1, Alt1)} <-
			+.lock,
			+position(Lat, Lon, Alt),
			-.lock;
		
		+.received(:tell, M): { ~ B flightPhase_message(M), ~B atc_message(M), ~B fuel_level_message(M), ~B position_message(M)} <- 
			+M;
			
			
		// Redundant Update
		+! changeFlightPhase(To) [perform] : {B flightPhase(To)};
			
		// Completely normal flight phase change - no emergencies - flight phase initiated by the environment.
		+!changeFlightPhase(To) [perform] : {~B equal(To, emergencyAvoid), B environment_flight_phase(To), B flightPhase(From), ~B flightPhase(emergencyAvoid)} <-
			+.lock,
			-flightPhase(From),
			+flightPhase(To),
			-.lock;

		// Completely normal flight phase change - no emergencies - flight phase initiated by agent.
		+!changeFlightPhase(To) [perform] : {~B equal(To, emergencyAvoid), ~B environment_flight_phase(To), B flightPhase(From), ~B flightPhase(emergencyAvoid), ~B equal(To, From)} <-
			+.lock,
			-flightPhase(From),
			+flightPhase(To),
			updateFlightPhase(To),
			-.lock;
	
		// Trying to change to emergency Avoid
		// Object no longer approaching - do nothing
		+!changeFlightPhase(emergencyAvoid) [perform] : {~B das_alert, B flightPhase(F)} <- 
			+.lock, 
			-enacting(emergencyAvoid), 
			updateFlightPhase(F),
			-.lock;

 		// Object no longer approaching - do nothing
		+!changeFlightPhase(emergencyAvoid) [perform] : {B das(objectPassed), B flightPhase(F), ~B flightPhase(emergencyAvoid)} <- 
			+.lock, 
			-enacting(emergencyAvoid),
			updateFlightPhase(F), 
			-.lock;
  
		// Stationary or (Grounded and alert500) - do nothing
		+!changeFlightPhase(emergencyAvoid) [perform] : {B das_alert_irrelevant, B flightPhase(F), ~B flightPhase(emergencyAvoid)} <- 
			+.lock, 
			-enacting(emergencyAvoid),
			updateFlightPhase(F), 
			-.lock;

 
  		// Old flight phase not yet stored
  		+!changeFlightPhase(emergencyAvoid) [perform] : {B flightPhase(F1), B das_alert, ~B das_alert_irrelevant, ~B store(flightPhase, F1), ~B flightPhase(emergencyAvoid)} <- 
			+.lock, 
			-enacting(emergencyAvoid), 
			+store(flightPhase,F1), 
			updateFlightPhase(emergencyAvoid),
			-.lock;
			
  		// Old flight phase stored
  		+!changeFlightPhase(emergencyAvoid) [perform] : {B flightPhase(F1), B das_alert, ~B das_alert_irrelevant, B store(flightPhase, F1), ~B flightPhase(emergencyAvoid)} <- 
			+.lock, 
			-flightPhase(F1),
			+flightPhase(emergencyAvoid),
			-.lock;

		// Trying to change from emergency Avoid
		// This is not the stored flight phase
		+! changeFlightPhase(To) [perform] : {B flightPhase(emergencyAvoid), B store(flightPhase, F), ~B equal(F, To)} <-
			+.lock,
			-store(flightPhase, F),
			+store(flightPhase, To),
			-.lock;

		+! changeFlightPhase(To) [perform] : {B flightPhase(emergencyAvoid), B store(flightPhase, To), ~B das_alert} <-
			+.lock,
			-store(flightPhase, To),
			-flightPhase(emergencyAvoid),
			+flightPhase(To),
			updateFlightPhase(F),
			-.lock;
			
		+! changeFlightPhase(To) [perform] : {B flightPhase(emergencyAvoid), B store(flightPhase, To), B das_alert_irrelevant} <-
			+.lock,
			-store(flightPhase, To),
			-flightPhase(emergencyAvoid),
			+flightPhase(To),
			updateFlightPhase(To),
			-.lock;

		// Can this even happen?  Trying to change ouot of emergencyAvoid while an object is aproaching
		+! changeFlightPhase(To) [perform] : {B flightPhase(emergencyAvoid), B store(flightPhase, To), B das_alert};
		+! changeFlightPhase(F2) [perform] : {B missionComplete};
		
				
		+!missionComplete [achieve] : {B flightPhase(unknown)} <- 
				requestFlightPhase,
				*flightPhase(waitingAtRamp); 
				
		+flightPhase(waitingAtRamp): { ~ B missionComplete} <-
				requestFuelStatus,
				requestPosition;
				
		+!missionComplete [achieve] : {B flightPhase(waitingAtRamp), ~B fuel(level, F)} <-
				*fuel(level, F);
				
		+!missionComplete [achieve] : {B flightPhase(waitingAtRamp), ~B position(Lat, Lon, Alt)} <-
				*position(Lat, Lon, Alt);
				
		+!missionComplete [achieve] : {B flightPhase(waitingAtRamp), ~B atc(taxiClearanceGiven(R))} <-
				-atc(N),
				requestTaxiClearance,
				*atc(M);
				
		+!missionComplete [achieve] : {B flightPhase(waitingAtRamp), B destination(D), B fuel(level, F), B position(Lat, Lon, Alt), B atc(taxiClearanceGiven(R)), ~B routes_stored_for_enaction} <-
				requestTaxiRoute(R, Lat, Lon, F),
				requestRoute(D, Lat, Lon, F),
				*enactRoute(taxi, NT),
				*enactRoute(cruise, NC);
				
		+route(taxi, N, T, F, S) : {True} <-
				requestEnactRoute(taxi, N);
		+route(cruise, N, T, F, S) : { True } <-
				requestEnactRoute(cruise, N);	
				
				
		+!missionComplete [achieve] : {B flightPhase(waitingAtRamp), B atc(taxiClearanceGiven(R)), B enactRoute(taxi, Num), ~B das(objectApproaching)} <-
				+.lock,
				+!changeFlightPhase(taxying) [perform],
				*flightPhase(holding);

		+!missionComplete [achieve] : {B flightPhase(holding), B enactRoute(cruise, N), ~B atc(lineUpClearanceGiven)} <-
				-atc(M1),
				requestLineUpClearance,
				*atc(M);
		
		+!missionComplete [achieve] : {B flightPhase(holding), B atc(lineUpClearanceGiven), ~B das(objectApproaching)} <-
				+.lock,
				+!changeFlightPhase(lineup) [perform],
				-.lock,
				*flightPhase(linedup);

		+!missionComplete [achieve] : {B enactRoute(cruise, R), B flightPhase(linedup), ~B atc(takeOffClearanceGiven)} <-
				-atc(N),
				requestTakeOffClearance,
				*atc(M);
				
		+!missionComplete [achieve] : {B enactRoute(cruise, R), B flightPhase(linedup), B atc(takeOffClearanceGiven), ~B das(objectApproaching)} <-
				+.lock,
				+!changeFlightPhase(takeOff) [perform],
				-.lock,
				*flightPhase(cruise);
				
		+flightPhase(cruise) : {B enactRoute(cruise, R), B enactRoute(taxi, N)} <-
				+.lock,
				-enactRoute(cruise, R),
				-enactRoute(taxi, N),
				-route(taxi, N, T, F, S),
				-route(cruise, R, T1, F1, S1),
				-atc(takeOffClearanceGiven),
				-.lock;
 				
		// das message handling
		+das(objectApproaching) : {~B flightPhase(emergencyAvoid), ~ B stationary} <- 
			+!reactToAlert [perform];
		+das(objectApproaching) : {B flightPhase(emergencyAvoid)} <- 
			-das(objectApproaching);
			
		+das(alert500) : {~B flightPhase(emergencyAvoid), ~ B on_ground} <- 
			+!reactToAlert [perform];
		+das(alert500) : {B flightPhase(emergencyAvoid)} <- 
			-das(alert(500));
			
		+das(objectPassed) : {B flightPhase(emergencyAvoid), B store(flightPhase,F)} <- 
			+.lock, 
			-das(objectApproaching),
			-das(alert500),
			-enacting(emergencyAvoid),
			-das(objectPassed), 
			updateFlightPhase(F),
			-.lock;
		
		+das(objectPassed) : {~ B flightPhase(emergencyAvoid) } <-
			+.lock,
			-! reactToAlert [perform],
			-das(objectApproaching),
			-das(alert500),
			-enacting(emergencyAvoid),
			-das(objectPassed), 
			-.lock;		
			
		+!reactToAlert [perform] : {True} <-
			requestEmergencyAvoid,		
			*enacting(emergencyAvoid),
			+! changeFlightPhase(emergencyAvoid) [perform];
					
			
		+!missionComplete [achieve] : {B das_alert, B store(flightPhase, F), ~B flightPhase(F)} <-
			*flightPhase(F);
		+!missionComplete [achieve] : {B das_alert, B store(flightPhase, F), B flightPhase(F)} <-
			*das(objectPassed);
		+!missionComplete [achieve] : {B flightPhase(emergencyAvoid), B store(flightPhase, F)} <-
			*flightPhase(F);
		+!missionComplete [achieve] : {B das_alert, B flightPhase(F), ~B store(flightPhase, F)} <-
			*das(objectPassed);


		// Reached destination
		
		+!missionComplete [achieve] : {B flightPhase(cruise), B destination(L), ~B location(L), ~B das_alert} <-
			*location(L);
		
		+!missionComplete [achieve] : {B flightPhase(cruise), B destination(L), B location(L), ~B fuel(level, F), ~B das_alert} <-
			*fuel(level, F);

		+!missionComplete [achieve] : {B flightPhase(cruise), B destination(L), B location(L), ~B position(Lat, Lon, Alt), ~B das_alert} <-
			*position(Lat, Lon, Alt);
			
		+!missionComplete [achieve] : {B flightPhase(cruise), B destination(L), B location(L), B fuel(level, F), B position(Lat, Lon, Alt), ~B appr(N, T, F1, S), ~B das_alert} <-
			requestApproach,
			*appr(N, T, F1, S);
					
		+!missionComplete [achieve] : {B flightPhase(cruise), B destination(L), B location(L), B appr(N, T, F, S), ~B enactAppr(N), ~B das_alert} <-
			enactApproach(N),
			*enactAppr(N);

		+!missionComplete [achieve] : {B flightPhase(cruise), B destination(L), B location(L), B enactAppr(N), ~B das_alert} <-
			+.lock,
			-appr(N, T, F, S),
			-enactAppr(N),
			+!changeFlightPhase(approach) [perform],
			-.lock,
			*flightPhase(approach);

		+veh(location,L) : {B destination(L)} <- 
			+.lock, 
			-veh(location,L), 
			+location(L), 
			-fuel(level, F),
			-position(Lat, Lon, Alt),
			requestFuelStatus,
			requestPosition,
			-.lock;    
		
		
		// Landing
		
		+!missionComplete [achieve] : {B flightPhase(approach), ~B das_alert} <-
			*flightPhase(landed);
		
		+!missionComplete [achieve] : {B flightPhase(landed), ~B fuel(level, F)} <-
			*fuel(level, F);
			
		+!missionComplete [achieve] : {B flightPhase(landed), ~B position(Lat, Lon, Alt)} <-
			*position(Lat, Lon, Alt);
			
		+!missionComplete [achieve] : {B flightPhase(landed), B fuel(level, F), B position(Lat, Lon, Alt), ~B route(taxi, Num, Time, Fuel, Safety)} <-
			requestTaxiRoute(destination, Lat, Lon, F),
			*enactRoute(taxi, Num);
				
		
		+!missionComplete [achieve] : {B flightPhase(landed), B enactRoute(taxi, Num), ~B das(objectApproacahing)} <-
			+.lock,
			+!changeFlightPhase(taxying) [perform],
 			-.lock,
			*flightPhase(waitingAtRamp);
		

		+veh(landed,L) : {True} <- 
			+.lock,
			-fuel(level, F),
			-position(Lat, Lon, Alt),
			+!changeFlightPhase(landed) [perform],
			-.lock,
			requestFuelStatus,
			requestPosition,
			+!completeLandingChecks [achieve];
					
		+!completeLandingChecks [achieve] : {B flightPhase(landed),  ~B fuel(level, L)} <-
			*fuel(level, L);

		+!completeLandingChecks [achieve] : {B flightPhase(landed),  ~B position(Lat, Lon, Alt)} <-
			*position(Lat, Lon, Alt);

		+!completeLandingChecks [achieve] : {B flightPhase(emergencyAvoid), B store(flightPhase, F)} <- 
			*flightPhase(F);
		
		+!completeLandingChecks [achieve] : {B das(objectApproaching), ~B flightPhase(emergencyAvoid)} <- 
			*das(objectPassed);

		 
		
				
		
				