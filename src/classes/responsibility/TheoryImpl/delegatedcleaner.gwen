GWENDOLEN

:name: manager

:Initial Beliefs:
//Possible Tasks task(name, [goals/actions], [statesOfTheWorld])
task(report, [reportToHuman], [])
task(safety, [], [isSafe])
task(clean, [clean1,clean2,clean3,clean4,clean5], [])
task(observer, [observe1,observe2,observe3,observe4,observe5], [allRoomsWillBeObserved])
 
//Possible Responsibilities res(name, [subres], task, [failres])
res(report, [], report, [])
res(safety, [clean, observe], safety, [report])
res(clean, [], clean, [])
res(observe, [], observer, []) 

//care values
care(clean,2)
care(safety,4)
care(observe,1)
care(report,10)

//Capacity - In essence pre-computed
capacity(reportToHuman)
capacity(safety)
capacity(isSafe)
capacity(clean)
capacity(observe)

//Working on task/res order
doAll
//doOne
//doRandomOne
cleanup(no)

//Current Responsibilities
currentRes([])
//Current Working Task
completedTasks([])
completedStates([])
completedRes([])

//Assumptions for testing
list1([a,b,c])
list2([c,a,b])
list3([a,b,c])

:Reasoning Rules:
dirt :- dirt_1;
dirt :- dirt_2;
dirt :- dirt_3;
dirt :- dirt_4;
dirt :- dirt_5;

//Get cared still not working
//getCared(L, Out) :- getCared3(L, Out, N);
//getCared3([], _, 0);
//getCared3([H|T], R, RC) :- getCared3(T, R, RC), care(H, HC), [HC<RC];
//getCared3([H|T], H, HC) :- getCared3(T, R, RC), care(H, HC), [RC<HC];

contains(_, [], false);
contains(Item,[Item],true);
contains(Item,[Item|T],true);
contains(Item,[Head|T],X) :- contains(Item,T,X);

:Initial Goals:
test[perform]

:Plans: 

//Received a delegated responsibility Example: assignment([ag1,ag2],safety)
+.received(:tell, assignment(Assignee,Res)): {True} <- +.lock, -ready, print("Received Assignment"), +!incomingRes(Assignee,[Res])[perform], +ready, -.lock;
+.received(:tell, X) : {True} <- print(X);

//Delegate
+!delegate(X,Ags)[perform] : {True} <- .send(Ags,:tell,X);

//Current Responsibility List Management
+!add(X)[perform] : {B currentRes(Z)} <- +.lock, -currentRes(Z), appendList(X,Z,Y), +currentRes(Y), -.lock;
+!rem(X)[perform] : {B currentRes(Z)} <- +.lock, -currentRes(Z), removeList(X,Z,Y), +currentRes(Y), -.lock;

//Tell assignee of acceptance
+!informAssignment([], Res)[perform] : {True} <- print("Informed all assignees");
+!informAssignment([Assignee|T], Res)[perform] : {True} <- prt("Informing acceptance to ", Assignee), .send(Assignee, :tell, accepted(Res, manager)), +!informAssignment(T,Res)[perform];

//Add Accepted Responsibility to list
+!addAcceptedRes(Assignee, NewRes)[perform] : {B currentRes(Res), B res(NewRes, SubRes, _, _)} <- +.lock, prt("Accepting Responsibility ", res), +assignment(Assignee,Res,manager), +!informAssignment(Assignee,Res)[perform], +!add([NewRes])[perform], -.lock;

//Decide if to accept responsibility
+!incomingRes(Assignee, [])[perform] : {True} <- +.lock, print("Processed all responsibilities"), -.lock;
+!incomingRes(Assignee, [Res|T])[perform] : {B capacity(Res), B res(Res,Sub,_,_)} <- +.lock, prt("Chose To Accept ", Res), +!addAcceptedRes(Assignee, Res)[perform], +!incomingRes(Assignee, T)[perform], +!incomingRes(Assignee, Sub)[perform], -.lock;
+!incomingRes(Assignee, [Res|T])[perform] : {~B capacity(Res)} <- +.lock, prt("Chose To Not Accept ", Res), +!incomingRes(Assignee, T)[perform], -.lock; //Should send message back

//Check if res is fulfilled, remove if true and report back
+!checkIfFulfilled(Res)[perform] : {B finished(Res)} <- +!rem([Res])[perform], reportBack(Res);
+!checkIfFulfilled(Res)[perform] : {B failed(Res), B res(Res,_,_,FailRes)} <- +!rem([Res])[perform], reportBack(Res), +!incomingRes(FailRes)[perform];
+!checkIfFulfilled(Res)[perform] : {True} <- prt("Worked on", Res);

//Action For Res
//Do all actions in a task before moving on to next res
+!doActionForRes(Res)[perform] : {B doAll, B res(Res,_,Name,_), B task(Name,[],_), ~B doneActions(true)} <- print("No actions to do"), +doneActions(true);
+!doActionForRes(Res)[perform] : {B doAll, B res(Res,_,Name,_), B task(Name,Actions,_), B completedTasks(Done), ~B doneActions(true)} <- +.lock, prt("Task", Name, " with Actions", Actions), getNextInList(Actions,Done,Task), do(Task), appendList([Task],Done,NDone), -completedTasks(Done), +completedTasks(NDone), doneAll(Actions,NDone,Finished), prt("Done Task ", Task, " Completed ", NDone), -doneActions(false), +doneActions(Finished), -.lock;

+!doActionForRes(Res)[perform] : {B doAll, B res(Res,_,Name,_), B task(Name,_,[]), ~B doneStates(true)} <- print("No states to do"), +doneStates(true);
+!doActionForRes(Res)[perform] : {B doAll, B res(Res,_,Name,_), B task(Name,_,States), B completedStates(Done), ~B doneStates(true)} <- +.lock, prt("Task" , Name, " with States", States), getNextInList(States,Done,State), check(State), appendList([State],Done,NDone),  -completedStates(Done), +completedStates(NDone), doneAll(States,NDone,Finished), prt("Done State ", State, " Completed ", NDone),-doneStates(false), +doneStates(Finished), -.lock;

+!doActionForRes(Res)[perform] : {B doAll, B doneActions(true), B doneStates(true), B completedRes(List)} <- +.lock, appendList([Res],List,NList), -completedRes(List), +completedRes(NList), prt("Completed Res are ", NList), -doneActions(true), -doneStates(true), -.lock;

+!doActionForRes(Res)[perform] : {True} <- prt("Could not find res ", res, " likely all res completed"); 
//Do one random action in a task before moving on

//Do one task in order before moving on

//Cleanup before repeating res loop
+!doCleanUp[perform] : {B completedRes(Res), B completedTasks(Tasks), B completedStates(States)} <- print("Cleaning up"), -completedRes(Res), -completedTasks(Tasks), -completedStates(States), +completedRes([]), +completedTasks([]), +completedStates([]), -cleanup(true);

//Responsibility Loop
+!test[perform] : {B cleanup(true)} <- +.lock, print("Do clean up"), +!doCleanUp[perform], -.lock, +!test[perform]; //, stop after one loop 
+!test[perform] : {B currentRes([])} <- print("No Responsibilities"), +!test[perform];
+!test[perform] : {~B ready} <- print("Not Ready Processing"), +!test[perform];
+!test[perform] : {B currentRes(ResList), B completedRes(Comp), B ready} <- +.lock, getCared(ResList, Res, Comp), prt("Responsibilities ",ResList," doing ", Res), +!doActionForRes(Res)[perform], +!checkIfFulfilled(Res)[perform], doneAll(ResList,Comp,Out), -cleanup(false), +cleanup(Out), +!test[perform], -.lock;
+!test[perform] : {True} <- print("Failed"), printstate;

